emulate -L zsh
.lzsh_preflight_check || return 1

# push new history file before reading command
fc -ap "${HISTFILE%.lazyshell*}.lazyshell-complete"

# Read user input
# Todo: use zle to read input
local REPLY
autoload -Uz read-from-minibuffer
read-from-minibuffer '> Query: ' || return 1

[[ -z $REPLY && -z $BUFFER ]] && return 1

# Save query to lazyshell history
print -rs - "$REPLY"

local os
.lzsh_get_distribution_name os

local intro="You are a zsh autocomplete script. All your answers are a single command${os:+ for $os}, and nothing else. You do not write any human-readable explanations. If you fail to answer, start your response with \`#\`."

if [[ -z $BUFFER ]]; then
  # No $BUFFER, simply pass query
  local prompt="$REPLY"
elif [[ -z $REPLY ]]; then
  # No query, simply ask to fix $BUFFER
  local prompt="Fix zsh command \`$BUFFER\`."
else
  local prompt="Alter zsh command \`$BUFFER\` to comply with query \`$REPLY\`"
fi

.lzsh_llm_api_call "$intro" "$prompt" "Query: $REPLY" || return

# if response starts with '#' it means GPT failed to generate the command
if [[ "$generated_text" == \#* ]]; then
  zle -M "$generated_text"
  return 1
fi

# Replace the current buffer with the generated text
BUFFER="$generated_text"
CURSOR=$#BUFFER

